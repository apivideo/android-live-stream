apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.dokka'
apply from: 'maven-push.gradle'


android {
    defaultConfig {
        minSdk rootProject.minSdk
        targetSdk rootProject.targetSdk
        compileSdk rootProject.compileSdk

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    namespace 'video.api.livestream'
}

dokkaHtml {
    moduleName.set("api.video Android $project.name library")
    suppressInheritedMembers.set(true)

    dokkaSourceSets {
        named("main") {
            noAndroidSdkLink.set(false)
            skipDeprecated.set(true)
            includeNonPublic.set(false)
            skipEmptyPackages.set(true)
        }
    }
    pluginsMapConfiguration.set(
            ["org.jetbrains.dokka.base.DokkaBase": """{ 
                "customAssets" : [
                    "${file("$rootDir/docs/docs_logo.svg")}",
                    "${file("$rootDir/docs/logo-icon.svg")}"
                ] 
        }"""]
    )
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    api "io.github.thibaultbee:streampack:$streamPackVersion"
    implementation "io.github.thibaultbee:streampack-extension-rtmp:$streamPackVersion"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}